<html>
<body>
<LINK REL="stylesheet" type="text/css" HREF="../default.css">
<head><title>/echo</title></head>
<H1>/echo</H1>
<br>
<br>
The <b>/echo</b> command prints text to the specified window. This command does not send anything to the server; text is only shown in the window.

<H4>Synopsis</H4>
<pre class ="code">
/echo [color-number] [-deghi[N]tsaqlbfnmr] [window] &lt;text&gt;
/echo -c[deghi[N]tsaqlbfnmr] &lt;color-name&gt; [window] &lt;text&gt;
</pre>

<H4>Switches</H4>

Target window switches
<br>

<br><b>-s</b>
      - prints to the status window
<br><b>-a</b>
      - prints to the active window
<br><b>-d</b>
      - prints to the single message window
<br>
<br>
Settings Related:
<br>

<br><b>-r</b>
      - Applies the strip settings to the text (as defined in mIRC Options / IRC / Messages).
<br><b>-l</b>
      - Applies the highlight settings to the text (as defined in mIRC Address Book / Highlight).
<br><b>-b</b>
      - Applies the windows beep settings (to beep if mIRC is not the active application) as defined in mIRC Options / Sounds and any individual window override.
<br><b>-f</b>
      - Applies the flash settings to the window (to flash the mIRC System Tray icon) as defined in mIRC Options / IRC / Options and any individual window override.
<br>
<br>
Other switches
<br>

<br><b>-c</b>
      - Indicates that the &lt;color-name&gt; parameter is provided as the 1st parameter following the switch(es).
<br><b>-e</b>
      - Encloses the text with a pair of lines containing the line separator. Note: does not add a leading line if it already consists entirely of the line separator.
<br><b>-g</b>
      - Prevents the text from being logged if the window has logging on.
<br><b>-h</b>
      - Applies a hard-wrap on the text so that the wrap point does not change when the window is resized. Text is not wrapped if it is echoed to a custom listbox window.
<br><b>-i</b>[N]
      - Indents wrapped lines by [N] characters. The default mIRC indent is 2 spaces if N not used. Note: When using a proportional font, the indented width is a constant width regardless of the width of the characters echoed.
<br><b>-t</b>
      - Adds a timestamp to the text if the timestamp is enabled for that window. Without this, the line will not be timestamped even if the window has timestamping set. Note: -t shows timestamp in Custom Windows even though they do not have a timestamp setting, but only if timestamping has been enabled in mirc-options/irc/messages/"timestamp events".
<br><b>-q</b>
      - Honors the $show flag. (Displays nothing if the alias was called with the . prefix)
<br><b>-n</b>
      - Prevents switchbar button/treebar color change
<br><b>-m</b>
      - Treats the line as an user message, colors the window button in the switchbar/treebar with the "Message" color defined in the option alt+o>Display.

<H4>Parameters</H4>
<b>[color-number]</b>
      - A numeric color value (0 to 99) Note: 99 is $color(listbox text) instead of $color(normal) even in #channel and Status window where background is $color(background). If $color(listbox text) is the same index as $color(background), 99 tries $color(normal) then $color(gray) searching for a color that doesn't match the background color.
<br><b>&lt;color-name&gt;</b>
      - When the -c switch is used, the next parameter must be one of the following: action, ctcp, highlight, info, info2, invite, join, kick, mode, nick, normal, notice, notify, other, own, part, quit, topic, wallops, whois, "listbox text", gray, listbox, "treebar text", treebar Note: Quotes are needed for items which are 2 words. Note: If color for that name matches $color(background) it is displayed as $color(normal) until the color index for background is changed.
     <br>Note: If color name not in the list of recognized names, defaults to 'normal'. Note: If you later change the color index for a color name, the echoed lines change to that color.
<br><b>[window]</b>
      - The target @window/#channel/query
<br><b>Note:</b> If you use /echo to send text to a custom @window which has been opened by <a href="window.html">/window -s</a> , the line will be added at the bottom of the window rather than being inserted in the correct sort position.
<br><b>Note:</b> If target is a listbox @window created using the -l switch, the output echoes to the Status Window instead.
<br><b>&lt;text&gt;</b>
      - The text to be printed

<br><b>Note:</b> regardless whether or not -e switch is used, echo will not create a 2nd consecutive line consisting of the line separator, unless the existing last line was created in a @window using /aline.

<H4>Example</H4>
1: Hello world

<pre class ="code">
/echo -a Hello World!
</pre>

2: A loop prints a few lines
<pre class ="code">
; This is example line 5.
; This is example line 4.
; This is example line 3.
; This is example line 2.
; This is example line 1.
alias example {
  var %x = 5
  while (%x) {
    echo -a This is example line $v1 $+ .
    dec %x
  }
}
</pre>
3: Timestamps
<pre class ="code">
echo #test This line never has a timestamp
echo -t #test This line has a timestamp only if timestamping is enabled within #test
echo #test $timestamp This line always has a timestamp
</pre>
4: Colours
<pre class ="code">
//echo 4 -at abc $chr(22) def $chr(15) The timestamp and this text following the Ctrl-O revert to color 4 (red)
//echo -act ctcp abc $chr(22) def $chr(15) The timestamp and this text following the Ctrl-O revert to "ctcp" color ( $+ $color(ctcp) $+ )
//echo -at $chr(3) $+ 04 $+ abc $chr(22) def $chr(15) The timestamp and this text appear as "normal" color ( $+ $color(normal) $+ )
</pre>
5: Switchbar/treebar colors
<pre class ="code">
; See mirc-options/display
; The 1st line causes the switchbar to change to the "event" color.
; The 2nd line -m causes the switchbar to change to the "messages" color, overriding the default EVENT color.
; The 3rd line -n leaves the switchbar color unchanged.
; Note that Chanserv giving the joined nick OP level is a separate event, and mIRC will change the -m override, and that event will color the switchbar as the EVENT color.

on ^*:JOIN:#:{
  echo $color(join) -t # * $nick $+($chr(40),$gettok($fulladdress,2-,33),$chr(41)) has joined # $comchan($nick,0)
  echo $color(join) -tm # * $nick $+($chr(40),$gettok($fulladdress,2-,33),$chr(41)) has joined # $comchan($nick,0)
  echo $color(join) -tn # * $nick $+($chr(40),$gettok($fulladdress,2-,33),$chr(41)) has joined # $comchan($nick,0)
  haltdef
}

</pre>
<pre class ="code">
/echo -m @window test
/echo -m #channel test
; has the same effect as:
/echo @window test
/echo #channel test
/window -g1 @window
/window -g1 #channel
</pre>
<pre class ="code">
; The echo command modifies &lt;text&gt; by hiding duplicate spaces and non-printable characters including $chr(9) tab.
//var %a abc $+ $chr(32) $+ $chr(32) $+ def | echo -a $len(%a) %a
; returns: 8 abc def
; ... where the duplicate space is not displayed.
//var %a abc $chr(9) def | echo -a $len(%a) %a
; returns: 9 abc def
; ... where there are 2 spaces appearing next to each other because they were not consecutive prior to the non-printable tab being hidden.
; All 6 letters are displayed on the same line because $crlf is non-printable:
//var %a $+(abc,$crlf,def) | echo -a %a
; Echo a blank line because it contains only non-printable characters:
//echo -a $crlf
</pre>

<H4>Compatibility</H4>
Added: mIRC v3.7 (24 Oct 1995)

<H4>Notes</H4>
None

<H4>See also</H4>
<a href="..\identifiers\colour.html">$colour</a><br>
<a href="..\identifiers\color.html">$color</a><br>
<!-- <a href="..\identifiers\n.html">$N</a><br> TODO ? -->
<a href="aline.html">/aline</a><br>
<a href="dline.html">/dline</a><br>
<a href="drawtext.html">/drawtext</a><br>
<a href="editbox.html">/editbox</a><br>
<a href="iline.html">/iline</a><br>
<a href="rline.html">/rline</a><br>
</body>
</html>
