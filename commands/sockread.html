<html>
<body>
<LINK REL="stylesheet" type="text/css" HREF="../default.css">
<head><title>/sockread</title></head>
<H1>/sockread</H1>
<br>
<br>
The <b>/sockread</b> command reads bytes from the receive buffer of a socket connection into a specified variable. This command must be and is to be used inside socket events.

mIRC in general only understand <a href="..\identifiers\crlf.html">$crlf</a> terminated line. With socket, mIRC only understands <a href="..\identifiers\lf.html">$lf</a> terminated line (any <a href="..\identifiers\cr.html">$cr</a> before a <a href="..\identifiers\lf.html">$lf</a> is removed).

The number of bytes waiting to be read can be retrieved using <a href="..\identifiers\sock.html">$sock(name).rq</a>
<br>
By default:
<br>
* /sockread &lt;%var&gt; reads a terminated line into the variable, if the received buffer does not contain a terminated line, no byte are read into the variable.
<br>* /sockread &var try to read 4096 bytes into the variable.

The real number of bytes read by /sockread can be retrieved using <a href="..\identifiers\sockbr.html">$sockbr</a>.

<H4>Synopsis</H4><pre class ="code">
/sockread [-fn] &lt;&lt;%var&gt;|&binvar&gt;
/sockread [numbytes] &lt;&lt;%var&gt;|&binvar&gt;
</pre>

<H4>Switches</H4>
<b>-f</b>
      - When reading a terminated line (/sockread &lt;%var&gt; or /sockread -n &var), forces mIRC to fill the variable with whatever text is in the receive buffer, even if it does not end in a terminated line.
<br><b>-n</b>
      - Reads a terminated line into a <a href="..\other\binary_variables.html">Binary Variables</a>. If the incoming line does not contain a terminated line, no byte will be read into the <a href="..\other\binary_variables.html">Binary Variables</a> , unless you specify the **-f** switch.

<H4>Parameters</H4>
<b>[numbytes]</b>
      - The number of bytes to read from the receive buffer.
<br><b>[&lt;%var&gt;/&binvar]</b>
      - The name of the variable to read the socket buffer into.
<br>

<H4>Reading</H4>

A single /sockread may not be enough to read the entire buffer (if you are reading line by line for example). You might want to keep reading until <a href="..\identifiers\sockbr.html">$sockbr</a> (the number of bytes read) is set to zero. This is far faster than letting mIRC re-trigger the event. If your script does not read the whole buffer, the on sockread event is re-triggered if:

    a) you were reading into a &binvar.

    b) you were reading into a &lt;%var&gt; and there is still a terminated line in the buffer waiting to be read.

This means you will miss the last line if it is not a terminated line and you are reading line by line only (and not in a binvar, although it's also easier to use on sockclose with binvar if you are reading line by line and facing this). You can read that line in the on sockclose event, :doc: `see below </commands/sockread#last_line_not_showing_up>` .

<br><b>Note:</b> (fixed since mIRC 7.36): Using SSL, on sockread might not be retriggered as it should, <a href="..\events\on_sockclose.html">On_SockClose</a> will be triggered too early with $sockerr sets to 3, you must read the receive buffer from that event

It is important to note that the data received will vary in size. Practically speaking, we receive several kilobytes per second, it is easy to assume that the data you want is going to be sent and received the same way and above all, the way you want, but that's incorrect, you might receive very small packet at a very small speed.

A common situation when dealing with HTML is to check the source of the page you are working with, to identify something that is unlikley to change, and to use that as a reference. Doing this is not bad, but the way it is usually implemented by mIRC user is incorrect.

The common errors are to blindly try to match what you saw in the source as you read it with your socket but also to try to read from the received buffer assuming it will always have the data already, let's consider the following source of a page:

"this is my reference:

I want that line"

There are two mistakes:

* Trying to match the reference as you are reading. Consider the following code


<pre class ="code">
on *:sockread:name:{
 var %a
 sockread -f %a
 if (%a == this is my reference:) {
    ...
 }
}
</pre>

If you do that, you cannot guarantee it will work 100% of the time.

If the received buffer is filled with a few bytes and ends up being "this is my ref" when on sockread triggers, the -f switch will force mirc to read that, and you won't be able to match your full line, for example here, the next time the event triggers, you might have a received buffer containing "erence:", but it might be "erence:\r\nI want that line" as well, screwing you anyway.

So you might think here "let's not use -f then":

<pre class ="code">
on *:sockread:name:{
 var %a
 sockread %a
 if (%a == this is my reference:) {
    ...
 }
}
</pre>
And that, assuming what you are looking for is not on the last non terminated line (which would be missed because -f is not there, although note that you can read that last non terminated line inside the <a href="..\events\on_sockclose.html">On_SockClose</a> event, should be fine. In fact, in this specific example and in general, it will work, because you are making sure %a is a full line or nothing. Well that's why it works in most situation, you are checking that %a is a specific text, which would fail if no byte were read into %a because a terminated line couldn't be found. However, if you are in a situation where you must check that %a is $null (usually because it read an empty $crlf line), you must check <a href="..\identifiers\sockbr.html">$sockbr</a> to know if you read bytes at all, a good example of this usage is shown below, which discard the headers of HTTP (check for an empty value after /Sockread %a reads an empty $crlf line):

<pre class ="code">
alias testHTTP {
sockclose testHTTP
sockopen testHTTP mirc.com 80
}

on *:sockopen:testHTTP:{
 if (!$sockerr) {
  sockwrite -n $sockname GET / HTTP/1.1
  sockwrite -n $sockname Host: mirc.com
  sockwrite -n $sockname
 }
}
on *:sockread:testHTTP:{
 if (!$sockerr) {
  if ($sock($sockname).mark) {
   ; here you can start reading the real source the way you want
  }
  else {
    var %a
    sockread %a
    if ($sockbr) {
     if (%a == $null) sockmark $sockname 1
    }
  }
 }
}
</pre>
Getting problem because you are not checking properly for $sockbr is very unlikely to happen if you are reading line by line, because it is much much slower than reading the whole content of the received buffer and as such, the received buffer is filled by mIRC faster than your socket code reads it. But it is still possible.

Another solution consists in making a buffer by yourself and adding what you are reading to it, until you get the correct portion you want (a terminated line in these examples).

If you are reading the whole content of the received buffer with one /sockread using binary variable or just large portion of the received buffer in a binary variable, you are more likely to see the

issue because you are basically reading the buffer as fast as mIRC fills it (or faster than line by line for large portion). The same solutions exists for binary variables, use /sockread -n to read a terminated line into the binvar, check $sockbr to make sure you read something etc. Using $bfind is the correct way to parse, unless you have very good evidence about the length of the lines you are going to receive and you want to go the easy way: if (text operator $bvar(&a,1,4096)) or similar.

* Reading from the receive buffer

This is the same as above but once you found the reference with the script, you want to grab the next line:

<pre class ="code">
on *:sockread:name:{
 var %a
 sockread %a
 if ($sockbr == 0) return
 if (%a == this is my reference:) {
    sockread %a
   echo -a my line: %a
 }
}
</pre>


The same issue can happen, you cannot make sure there is a terminated line in the receive buffer. People mainly uses that because it avoids saving the state for the next time the event retriggers, indeed the correct way to read the next line once you found the reference is to try reading a terminated line using /sockread &lt;%var&gt; (or to use your own buffer and check when you have a new line, just like above) but you need to save the state if you can't find the next line currently:

<pre class ="code">
on *:sockread:name:{
 var %a
 sockread %a
 if ($sockbr == 0) return
 if (%tryingnextline) { echo -a my line: %a | unset %tryingnextline | return  }
 if (%a == this is my reference:)  {
    set %tryingnextline 1
    sockread %a
    if ($sockbr) {
     echo -a my line: %a
     unset %tryingnextline
   }
 }
}
</pre>

<br><b>General Note:</b> it can be a good idea and might be simpler for you to read everything to a file and then parse that file.

<H4>Last line not showing up</H4>

Another common problem is reading the last line sent by an HTTP server, which isn't a terminated line (no $crlf or $lf).

Indeed, if you are using /sockread &lt;%var&gt;, you're will read properly line by line but that last line won't be read by this sockread command.

We also saw how this non terminated line in the received buffer wouldn't make mIRC retrigger the on sockread event.

One solution which doesn't involve more than that, is to read that line from the on sockclose event, indeed you are sure on sockread read the previous line, so inside on sockclose, you should get only that last non terminated line, this time we use the -f switch to force the read:

<pre class ="code">
on *:sockclose:name:{
  if (!$sockerr) {
   var %a
   sockread -f %a
   if ($sockbr == 0) return
   echo -a > %a
 }
}
</pre>

If you are using HTTP 1.1 and you actually want the socket to remain open, you would need to grab the value of the content-length header, store that in a variable and increase another variable with the length of what you are reading, if the length of the received buffer + the value of that variable equal the value of the content length, you should first try to see if you have a line by reading with /sockread %a, and if no byte is read, then use /sockread -f %a.

<a name="example"></a>
<H4>Example</H4>

Here is an example which will read and echo to the status window what is sent by a server line by line.

<br><b>Note:</b> leading/consecutive and trailing spaces and non-printable characters won't be shown correctly.

<br><b>Note2:</b> too long line will produce an error.

<br><b>Note3:</b> Using SSL, the on sockread event might not be triggered though it should, you must read the rest in the on sockclose event (has been fixed since mIRC 7.36).

<pre class ="code">
ON *:SOCKREAD:mySocket:{
  var %a
  if ($sockerr > 0) { return }
  sockread %a
  if (!$sockbr) return
  while ($sockbr) {
    echo -a > %a
    sockread %a
  }
}
</pre>
<br><b>Note:</b> as we just saw, if the last line in the source does not end with a terminated line, it won't be read by that event.

If you ever worked with HTTP 1.1, you know that it can send data in chunk, here is a way to write the real content to a file:

<pre class ="code">
on *:sockopen:socket:{
  ... your request...
  sockmark $sockname 0
  ;we write to source.txt
  .remove source.txt
  unset %bytestoread
}
on *:SOCKREAD:socket: {
  if (!$sockerr) {
    if ($sock($sockname).mark) {
      ;if we have a chunk to read
      if (%bytestoread > 0) {
        ;we try to read that much
        sockread %bytestoread &a
        bwrite source.txt -1 -1 &a
        ;but $sockbr tells us how much we read, we decrease by that number (in case we received something smaller than what we want, this part of the code will retrigger in this case, until %bytestoread is 0)
        dec %bytestoread $sockbr
      }
      ;if we don't have a chunk
      else {
        var %a
        sockread %a
        ;skip empty line
        while ($sockbr) && (%a == $null) { sockread %a }
        if (!$sockbr) || (%a == $null) return
        ;last chunk
        if (%a == 0) echo -a done
        ;convert the hexadecimal number to decimal
        else { set %bytestoread $base(%a,16,10) }
      }
    }
    else {
      ;$sock().mark is used to discard the headers
      var %a
      sockread %a
      if (%a == $null) sockmark $sockname 1
    }
  }
}
</pre>

<H4>Compatibility</H4>
Added: mIRC v5.3 (04 Jan 1998)

<H4>Notes</H4>
None

<H4>See also</H4>
<a href="..\identifiers\sock.html">$sock</a><br>
<a href="..\identifiers\sockname.html">$sockname</a><br>
<a href="..\identifiers\sockerr.html">$sockerr</a><br>
<a href="..\identifiers\sockbr.html">$sockbr</a><br>
<a href="sockaccept.html">/sockaccept</a><br>
<a href="sockclose.html">/sockclose</a><br>
<a href="socklist.html">/socklist</a><br>
<a href="socklisten.html">/socklisten</a><br>
<a href="sockmark.html">/sockmark</a><br>
<a href="sockopen.html">/sockopen</a><br>
<a href="sockpause.html">/sockpause</a><br>
<a href="sockrename.html">/sockrename</a><br>
<a href="sockudp.html">/sockudp</a><br>
<a href="sockwrite.html">/sockwrite</a><br>
</body>
</html>
