<html>
<body>
<LINK REL="stylesheet" type="text/css" HREF="../default.css">
<H1>$nick</H1>
<br>
<br>
The <b>$nick</b> identifier can be called in two ways:

# '''$nick''' will return the nickname associated with an IRC event.
# '''$nick(#channel,index)''' retrieves information about nicknames on a specific channel.

<H4>Synopsis</H4>
<pre class ="code">
$nick
$nick(#,N/nick,aohvr,aohvr)
</pre>
<H4>Parameters</H4>
<b>#</b>The channel name to look up nickname information.

<br><b>N/nick</b>Can be either a number to look up a particular Nth user (if N is 0, returns total matching users based on criteria), or can be a nickname to target a specific user.

<br><b>aohvr</b>Optional: The first '''aohvr''' characters target all users of a '''specific type''', while the second '''aohvr''' excludes a '''specific type''' from the target list.

=== Specific Types ===
<br><b>a</b>All users
<br><b>o</b>Operators
<br><b>h</b>Helpers
<br><b>v</b>Voiced
<br><b>r</b>Regular users


Except for Type 'a' which has been defined as all users in the channel, channel modes given to users and pnick characters can also be used as nick types. This includes networks where channel owner is given mode +q and pnick character ! or ~, and superop is given mode +a and pnick character &. This next table assumes channel is at a network supporting the additional channel modes given to nicks.

<br>
<br><b>q</b> Channel Owner having mode +q
<br><b>~</b> Channel Owner having pnick character ~
<br><b>&</b> SuperOp (a.k.aa Protected Op)  having pnick character &
<br><b>@</b> Operators
<br><b>%</b> Helpers
<br><b>+</b> Voiced



<H4>Properties</H4>
<b>.pnick</b> - returns the target result with their elevation level, eg: @,+,%. Regular users display normal.
<br><b>.idle</b> - returns the current idle time for the nickname on the channel specified
<br><b>.color</b> - returns the color for that user as set up in the Nick color dialog

<H4>Example</H4>
Echo to the active screen the total amount of users in the channel:
<pre class ="code">//echo -a Total users: $iif($active ischan,$nick(#,0))</pre>

Echo to the active screen the number of operators in the channel:
<pre class ="code">//echo -a Total users: $iif($active ischan,$nick(#,0,o))</pre>

Echo to the active screen all elevated users only, excluding regular users:
<pre class ="code">//echo -a Total users: $iif($active ischan,$nick(#,0,a,r))</pre>

Create an alias that will open a [[Custom windows - mIRC|custom @window]], and detail the user list:
<pre class ="code">alias getUserDetails {

  ; Make sure the active window is a channel
  if ($active ischan) {

    ; Set the %c variable to the channel name
    var %c = $active
    window @getUserDetails 350 350 650 300

    ; Clear the window justin in case it
    ; was already opened previously
    clear @getUserDetails

    ; Echo all of the user details
    echo @getUserDetails Channel: %c
    echo @getUserDetails $crlf $crlf
    echo @getUserDetails Total Users: $nick(%c,0)
    echo @getUserDetails Operators: $nick(%c,0,o)
    echo @getUserDetails Voices: $nick(%c,0,v)
    echo @getUserDetails Helpers: $nick(%c,0,h)
    echo @getUserDetails Regular Users: $nick(%c,0,r)
    echo @getUserDetails $crlf $crlf

    ; Below lists elevated users, whom are the ops/voices/helpers
    echo @getUserDetails Total Elevated Users: $nick(%c,0,a,r)
    echo @getUserDetails $crlf $crlf

    ; Lastly, list the nickname of the first nickname in the
    ; op list, voice list, helper list, and regular user list
    echo @getUserDetails First operator/voice/helper/regular users:
    echo @getUserDetails $lf $iif($nick(%c,1,o),$v1,NONE)
    echo @getUserDetails $lf $iif($nick(%c,1,v),$v1,NONE)
    echo @getUserDetails $lf $iif($nick(%c,1,h),$v1,NONE)
    echo @getUserDetails $lf $iif($nick(%c,1,r),$v1,NONE)
  }
}</pre>

The above command is executed in a channel command-line by typing:
/getUserDetails

The results from the above will look something like this:

[[File:$nick identifer example results - mIRC.png|Shows the example results.]]

'''Note:''' A nick can have several channel modes, and the nicklist will display only the highest level. To have an exact count of the nicks displaying a certain nick type, you should have the 3rd parameter be the list of all types of that level and greater, then have the 4th parameter be the nick types greater than the specified level. For example, networks supporting levels above @Operator also give +o. ~ChannelOwner is given modes +qo and &ProtectedOp is given mode +ao. Some networks allow the Founder to give +q to several other users, so the ~ prefix isn't always an indication that someone is the channel owner/founder.

//echo -a There are $nick($chan,0,~&o,~&) nicks at the @operator level excluding Owners and SuperOps

<H4>Compatibility</H4>
Added: mIRC v2.1a (28 Feb 1995)

<H4>Notes</H4>
None
<H4>See also</H4>
<a href="chan.html">$chan</a><br>
{{mIRC|on events}}

</body>
</html>
