<html>
<body>
<LINK REL="stylesheet" type="text/css" HREF="../default.css">
<head><title>$urlget</title></head>
<H1>$urlget</H1>
<br>
<br>
The <b>$urlget</b> identifier sends a HEAD/GET/POST/PUT/PATCH/DELETE to an http server. Returns an ID. Can also be used to get information about current running $urlget.


<H4>Synopsis</H4>
<pre class ="code">
$urlget(url,hgpuadfbtik,&lt;target&gt;,&lt;alias&gt;,[&headers],[&body])
$urlget(N/ID,cr)
$urlget(N/ID)[.prop]
</pre>

<H4>Parameters</H4>
<b>url</b> - The url to access, must be prefixed with http:// or https:// (maximum url characters are <b>2000</b>)
<br><b>hgpuadfbrtic</b> - Switches to indicate how to proceed:
<br><b>hgpuad</b> - '<b>h</b>' for HEAD, '<b>g</b>' for GET, '<b>p</b>' for POST, '<b>u</b>' for PUT, '<b>a</b>' for PATCH, '<b>d</b>' for DELETE.
<br><b>fb</b> - '<b>f</b>' to output the answer (without the responses headers) to a file, '<b>b</b>' to output to a binvar.
<br><b>r</b> - Resumes the processing.
<br><b>t</b> - Uses .part file if necessary.
<br><b>i</b> - Ignores SSL errors.
<br><b>c</b> - Cancel the processing.
<br><b>k</b> - prevent redirection.
<br><b>target</b> - The output: the filename if you used <b>f</b>, or a &binvar for <b>b</b>.
<br><b>alias</b> - The name of the alias that is called at the end, this alias is called as a command with one parameter passed to it: the ID.
<br><b>headers</b> - Optional, a &binvar containing the headers to be sent, separated by $crlf.
<br><b>body</b> - Optional, a &binvar containing the data to be sent when you are POSTing.
<br><b>ID/N</b> - The Nth urlget or the urlget referenced by the ID.

<H4>Properties</H4>
<b>.url</b> - Returns the url used.
<br><b>.redirect</b> - Returns the value of the location header if available.
<br><b>.method</b> - Returns the method used (GET/POST).
<br><b>.type</b> - Returns the type of output, (binvar/file).
<br><b>.target</b> - Returns the value of target (name of the binvar or filename).
<br><b>.alias</b> - Returns the name of the alias used.
<br><b>.id</b> - Returns the ID.
<br><b>.state</b> - Returns the state <b>ok</b>, <b>connect</b>, <b>download</b>, or <b>fail</b>.
<br><b>.size</b> - Returns the value of the content-length header.
<br><b>.resume</b> - Returns <b>0</b> or <b>1</b> depending on if you are resumed/paused?
<br><b>.rcvd</b> - Returns the number of bytes received after the header, could be different from .size after a failed download.
<br><b>.time</b> - Returns the time taken to complete the processing, in millisecond.
<br><b>.reply</b> - Returns the response headers.

<H4>Example</H4>
Usage: /download https://www.mirc.com/versions.txt
<pre class ="code">
alias download {
  if (!$1) { return }

  if ($1 isnum) {
    if ($urlget($1).state == fail) { echo 4 -at Error: Connection issues! | return }

    .run notepad.exe $qt($urlget($1).target)
  }
  else {
    var %file = source.txt

    write -c $qt(%file)

    return $urlget($1,gfi,%file,download)
  }
}
</pre>

Another example

<pre class ="code">
; Type /urlget.test
;
; > POST / HTTP/1.1
; > Accept: */*
; > Test: Header
; > Accept-Encoding: gzip, deflate
; > User-Agent: mIRC
; > Host: localhost
; > Content-Length: 19
; > Connection: Keep-Alive
; > Cache-Control: no-cache
; > foo1=bar1&foo2=bar2
; < HTTP/1.1 200 OK
; < Connection: close
; < Content-Length: 5
; < hello
; url      http://localhost/
; redirect http://localhost/
; method   post
; type     binvar
; target   &target
; alias    urlget.callback
; id       1027
; state    ok
; size     5
; resume   0
; rcvd     5
; time     125
; reply    HTTP/1.1 200 OKConnection: closeContent-Length: 5
; response hello



alias urlget.test {
  urlget.listen
  var %url = $iif($1,$1,http://localhost/)
  bset -t &header 1 Test: Header
  bset -t &body 1 foo1=bar1&foo2=bar2

  var %id = $urlget(%url,pb,&target,urlget.callback,&header,&body)
}

alias urlget.callback {
  var %id = $1

  echo -agi9 url      $urlget(%id).url
  echo -agi9 redirect $urlget(%id).redirect
  echo -agi9 method   $urlget(%id).method
  echo -agi9 type     $urlget(%id).type
  echo -agi9 target   $urlget(%id).target
  echo -agi9 alias    $urlget(%id).alias
  echo -agi9 id       $urlget(%id).id
  echo -agi9 state    $urlget(%id).state
  echo -agi9 size     $urlget(%id).size
  echo -agi9 resume   $urlget(%id).resume
  echo -agi9 rcvd     $urlget(%id).rcvd
  echo -agi9 time     $urlget(%id).time
  echo -agi9 reply    $urlget(%id).reply

  if ($urlget(%id).type == binvar) {
    echo -agi9 response $bvar($urlget(%id).target,1-3000).text
  }
}

alias urlget.listen {
  if (!$sock(urlget.listen)) socklisten -d 127.0.0.1 urlget.listen 80
}

on *:socklisten:urlget.listen:{
  var %sockname = urlget.client. $+ $ticks
  if ($sock(%sockname)) return

  sockaccept %sockname
}

on *:sockread:urlget.client.*:{
  var %header

  if (!$sock($sockname).mark) {
    sockread %header
    while (%header != $null) {
      echo 3 -ag > %header
      if ($regex(%header,Content-Length: (\d+))) {
        hadd -m $sockname content-length $regml(1)
      }
      sockread %header
    }
    if ($sockbr) sockmark $sockname $true
  }

  if ($sock($sockname).mark) && ($sock($sockname).rq) {
    sockread &read

    while ($sockbr) {
      hinc $sockname content-read $sockbr
      echo 6 -agi2 > $bvar(&read,1-3000).text

      sockread &read
    }
  }

  if ($hget($sockname,content-length) == 0) || ($v1 == $hget($sockname,content-read)) {
    socket.respond $sockname hello
    hfree -w $sockname
  }
}

alias -l sockwrite {
  echo 12 -ag &lt; $3-
  sockwrite $1-
}

alias -l socket.respond {
  var %sockname = $$1, %data = $2-
  sockwrite -n %sockname HTTP/1.1 200 OK
  sockwrite -n %sockname Connection: close
  sockwrite -n %sockname Content-Length: $len(%data)
  sockwrite -n %sockname $+($crlf,%data)
}
</pre>

cancelling and resuming
<pre class ="code">
;cancel the first $urlget request currently running
$urlget(1,c)


;resume the request identified by the id &lt;id&gt;
$urlget(&lt;id&gt;,r)
</pre>

<H4>Compatibility</H4>
Added: mIRC v7.56 (06 Jul 2019)
<br>
<br>
v7.56: Added .alias, .binvar, .dir, .error, .file, .id, .method, .rcvd, .redirect, .reply, .resume, .size, .state, .status, .target, .time, .type, and .url properties.
<H4>Notes</H4>
None

<H4>See also</H4>
<a href="..\commands\url.html">/url</a><br>
<a href="url.html">$url</a><br>
</body>
</html>
